import groovy.text.SimpleTemplateEngine

plugins {
    id 'com.gradleup.shadow'
}

// 设置生成的 jar 文件名称
ext.platform = project.name
String pattern__name = rootProject.properties['name_template']
String pattern__ver = rootProject.properties['ver_template']
// 包名
archivesBaseName = new SimpleTemplateEngine()
        .createTemplate(pattern__name)
        .make(rootProject.properties + [platform: ext.platform])
        .toString()
// 版本
version = new SimpleTemplateEngine()
        .createTemplate(pattern__ver)
        .make(rootProject.properties)
        .toString()

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}


shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

// 让 NeoForge 子项目中嵌入 common 下的资源
sourceSets.main.resources {
    // 将 common 模块的 resources 目录也视为资源目录 (即 ~/main/resources)
    srcDir project(':common').file('src/main/resources')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
//        expand version: inputs.properties.version
        expand project.ext.properties
    }

    // 排除资源目录中的文件 (会考虑上面额外引入的外部目录)
    exclude 'i_feel_bad.mixins.json'
}

